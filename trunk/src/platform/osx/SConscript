# ----------------------------------------------------------------------------
#
#   enso_osx.graphics SConscript
#
# ----------------------------------------------------------------------------

# ----------------------------------------------------------------------------
# Imports
# ----------------------------------------------------------------------------

Import( "env" )

import os
import sys
import subprocess


# ----------------------------------------------------------------------------
# Helper Functions
# ----------------------------------------------------------------------------

def getOutput( params ):
    """
    Runs the given program, as specified by the parameter list.  Raises an
    exception with stderr output if the process has a nonzero return
    code.  Otherwise, it returns the stdout of the process as a string.
    """

    popen = subprocess.Popen( params,
                              stdout=subprocess.PIPE,
                              stderr=subprocess.PIPE )
    output, errors = popen.communicate()
    if popen.returncode != 0:
        sys.stderr.write( "Running '%s' failed.\n" % " ".join(params) )
        sys.stderr.write( "Output was:\n%s\n(end of output)\n" % errors )
        raise SystemError()
    return output


# ----------------------------------------------------------------------------
# Build Actions
# ----------------------------------------------------------------------------

# cairo

SConscript( "cairo/SConscript", exports="env" )
Import( "cairo" )

env = env.Copy()

env.Append( CPPPATH = [cairo["include_dir"]], LIBPATH = [cairo["lib_dir"]] )

# Flag needed for Python C extension modules, so that missing
# Python symbols are looked-up (and found) when the module is loaded.
FIND_MISSING_SYMS_AT_RUNTIME_FLAGS = ["-undefined", "dynamic_lookup"]

# quartz cairo bridge

qcbEnv = env.Copy()

qcbEnv.Append(
    CPPPATH=["pycairo"],
    LINKFLAGS=FIND_MISSING_SYMS_AT_RUNTIME_FLAGS,
    LIBS=[cairo["lib"]],
    FRAMEWORKS=["AppKit"],
    )

quartzCairoBridge = qcbEnv.LoadableModule(
    source = ["quartz_cairo_bridge.m"],
    target = ["quartz_cairo_bridge.so"],
    )

qcbEnv.Install( "#enso/platform/osx", quartzCairoBridge )

# key notifier

keyNotifier = env.Program(
    source = ["EnsoKeyNotifier.m"],
    FRAMEWORKS = ["ApplicationServices", "Foundation"]
    )

env.Install( "#bin", keyNotifier )

# key utils

keyUtils = env.LoadableModule(
    source = ["key_utils.m"],
    target = ["key_utils.so"],
    FRAMEWORKS = ["ApplicationServices"]
    )

env.Install( "#enso/platform/osx/selection", keyUtils )

# Pycairo

pycairoEnv = env.Copy()

pycairoEnv.Append( LINKFLAGS=FIND_MISSING_SYMS_AT_RUNTIME_FLAGS )

# Pycairo may raise warnings, and that's okay--it's not our code.
pycairoEnv["CCFLAGS"].remove( "-Werror" )

SConscript( "pycairo/SConscript", exports="pycairoEnv" )

# App

enso = env.Program( "main.m",
                    FRAMEWORKS=["Cocoa", "Python"] )
env.InstallAs( "#bin/Enso.app/Contents/MacOS/Enso", enso )
env.Install( "#bin/Enso.app/Contents/Resources/bin",
             keyNotifier )
env.InstallAs( "#bin/Enso.app/Contents/Resources/main.py",
               "#scripts/run_enso.py" )

if env["STANDALONE"]:
    env.Install( "#bin/Enso.app/Contents/MacOS", cairo["libs"] )

SConscript( "appdata/SConscript", exports="env" )

# Wanted to use the Mkdir() action here, but it's throwing some
# bizarre exception, so we'll just use the command line.
Execute( "mkdir -p %s" % Dir("#bin/Enso.app/Contents/Resources/enso") )

basepath = Dir("#").abspath
ensoPkgDir = Dir("#enso").abspath
ensoAppBundleDir = basepath + "/bin/Enso.app/Contents/Resources/enso"

Execute( "rm -rf %s" % ensoAppBundleDir )

if env["STANDALONE"]:
    # TODO: This needs to be fixed; we're copying the OLD contents
    # of the enso directory before we actually carry out our SCons
    # actions!
    Execute( "cp -R %s %s" % (ensoPkgDir, ensoAppBundleDir) )
else:
    Execute( "ln -s -f %s %s" % (ensoPkgDir, ensoAppBundleDir) )
